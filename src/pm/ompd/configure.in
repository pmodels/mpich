dnl Process this file with autoconf to produce a configure script.
# Autoconf initialization:
AC_INIT(mpdroot.h)
AC_PREREQ(2.59)
# Autoconf header:
rm -f ${srcdir}/mpdconf.h mpdconf.h
AC_CONFIG_HEADER(mpdconf.h)
AH_TOP([/* -*- Mode: C; c-basic-offset:4 ; -*- */
/*  
 *  (C) 2001 by Argonne National Laboratory.
 *      See COPYRIGHT in top-level directory.
 */
#ifndef MPDCONF_H_INCLUDED
#define MPDCONF_H_INCLUDED
])
AH_BOTTOM([#endif])

dnl Process configure arguments
AC_ARG_ENABLE(echo,
   [--enable-echo - turn on echoing from within configure script],
   set -x)
CONFIGURE_ARGS="$*"
if test -n "$CONFIGURE_ARGS" ; then 
    echo "Configuring with args $CONFIGURE_ARGS"
fi
AC_SUBST(COPTIONS)
dnl add --enable-strict and --enable-g

AC_ARG_ENABLE(g, [--enable-g  - turn on debugging flags ] )
if test "$enable_g" = "yes" -o "$enable_g" = "all" ; then
    COPTIONS="${COPTIONS} -g"
fi

dnl Checks for programs.
dnl Determine the executable and object file extensions.  These
dnl are needed for some operations under cygwin
AC_EXEEXT
AC_OBJEXT
# Checking for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_LN_S
# A quick check to ensure that install-sh has the execute bit set
# (we've had some problems with that)
if test -f install.sh -a ! -x install.sh ; then chmod +x install.sh ; fi
AC_PROG_INSTALL

MPD_AC_PROG_CHECK_INSTALL_WORKS

if test "$installOk" = "no" ; then
    # echo "RMB1: SETTING install"
    INSTALL="${srcdir}/install.sh"
fi
echo Using INSTALL=$INSTALL

dnl Find a Python
AC_CHECK_PROG(PY23, python2.3, python2.3)
AC_CHECK_PROG(PY2,  python2, python2)
AC_CHECK_PROG(PY22, python2.2, python2.2)
AC_CHECK_PROG(PY,   python, python)
if test -z "$PY23"  -a  \
        -z "$PY2"   -a  \
        -z "$PY22"  -a  \
        -z "$PY" ; then
    AC_MSG_WARN([Could not find a python executable in path: $PATH]) 
fi

dnl See if one of the pythons has the right modules
PYTHON=""
for pypgm in $PY23 $PY2 $PY22 $PY ; do \
    AC_MSG_CHECKING([that $pypgm is a valid python version])
    changequote(,)dnl
    py_version=`$pypgm -c "import sys;print sys.version_info[0]"`
    py_subversion=`$pypgm -c "import sys;print sys.version_info[1]"`
    changequote([,])dnl
    if test "$py_version" -gt "2" -o "$py_version" -ge "2" -a "$py_subversion" -ge "2" ; then
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
	continue
    fi	       
    
    AC_MSG_CHECKING([that the cPickle module is available for $pypgm])
    if $pypgm -c 'from cPickle import dumps'  >/dev/null 2>&1 ; then
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
	continue
    fi
    
    AC_MSG_CHECKING([that the xml minidom module is available for $pypgm])
    if $pypgm -c 'from xml.dom.minidom import parseString'  >/dev/null 2>&1 ; then
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
	continue
    fi

    AC_MSG_CHECKING([that the xml minidom module works])
    if $pypgm -c 'from xml.dom.minidom import parseString; parseString("<x>x</x>")'  >/dev/null 2>&1 ; then
        AC_MSG_RESULT(yes)
    else
        AC_MSG_RESULT(no)
	continue
    fi
    PYTHON="$pypgm"
    break
done
#
if test -z "$PYTHON" ; then
    AC_MSG_ERROR([Python required (version 2.2 or greater, with necessary modules)]) 
fi

CYGWIN_TEST=`uname -a | grep -i cygwin`
if test -n "$CYGWIN_TEST" ; then
    AC_MSG_CHECKING([that rebaseall is ok on this cygwin system])
    echo
    if ! $PYTHON -c 'from socket import socket ; from os import fork ; x = fork()'  >/dev/null 2>&1 ; then
        AC_MSG_ERROR([*** fork may fail on this cygwin system; you may need to run rebaseall]) 
    fi
fi

# echo "RMB PYTHON=" $PYTHON
AC_SUBST(PYTHON)

# In case clinker is not defined
if test -z "$CLINKER" ; then 
    CLINKER="$CC"
    AC_SUBST(CLINKER)
fi

dnl Checks for libraries.
# Searching libraries instead of checking
AC_SEARCH_LIBS(socket, socket)

dnl Checks for header files.
# Checking headers
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(fcntl.h strings.h sys/time.h unistd.h stdlib.h)

dnl Checks for library functions.
# Checking library functions
AC_CHECK_FUNCS(putenv socket)

dnl Output:
# Starting output
MPD_HOME=`pwd`
AC_SUBST(MPD_HOME)
dnl
dnl Dependency handling
AC_SUBST(MAKE_DEPEND_C)
# AC_OUTPUT_COMMANDS(chmod +x startdaemons)
AC_OUTPUT(Makefile)
